apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def versionMajor = 0
def versionMinor = 6
def versionPatch = 0

group 'ka.masato.library.device'
archivesBaseName = 'grideyelib'
version "${versionMajor}.${versionMinor}.${versionPatch}"

def libraryId = "grideyelib"
def siteUrl = 'https://github.com/masato-ka/grideyelib'
def githubUrl = 'https://github.com/masato-ka/grideyelib'

android {
    compileSdkVersion 28



    defaultConfig {
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 1
        versionName project.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.8.4'
    testImplementation 'com.google.android.things:androidthings:+'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.google.android.things:androidthings:+'
    compileOnly 'com.google.android.things:androidthings:+'
}

bintray {
    
    user = project.hasProperty("bintray_user") ? bintray_user : BINTRAY_USER
    key = project.hasProperty("bintray_key") ? bintray_key : BINTRAY_KEY
    configurations = ['archives']

    pkg {
        repo = 'android-things-support'
        name = project.group + '.' + libraryId
        licenses = ['MIT']
        websiteUrl = siteUrl
        vcsUrl = githubUrl + '.git'
        issueTrackerUrl = githubUrl + '/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name libraryId
            url siteUrl

            packaging 'aar'
            groupId project.group
            artifactId libraryId
            version project.version

            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                connection githubUrl + '.git'
                url githubUrl
            }
        }
    }
}